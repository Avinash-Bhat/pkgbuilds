# Maintainer: Moritz Lipp <mlq@pwmt.org>
# Contributor: gns_ank <anikevicius@gmail.com>

_gitname='zathura'

pkgname=zathura-git
pkgver=2013.11.05.g903dae5
pkgrel=1
pkgdesc="a document viewer"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura"
license=('custom')
depends=('girara-gtk3-git' 'gtk3>=3.0.0' 'cairo>=1.8.8')
makedepends=('git' 'python-docutils' 'intltool')
conflicts=('zathura')
provides=('zathura')
source=("git://pwmt.org/${_gitname}.git#branch=develop")
md5sums=('SKIP')

optdepends=(
  'zathura-pdf-poppler-git: PDF support by using poppler'
  'zathura-pdf-mupdf-git: PDF support by using mupdf'
  'zathura-djvu-git: djvu support by using djvulibre'
  'zathura-ps-git: PostSctipt support by using libspectre'
)

pkgver() {
  cd "${srcdir}/${_gitname}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  # install files
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
