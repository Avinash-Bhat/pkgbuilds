# Maintainer: mlq <mlq@pwmt.org>
# Contributor: gns_ank <anikevicius@gmail.com>

_girara_gtk_version=3
_gitname='girara'

pkgname=girara-gtk$_girara_gtk_version-git
pkgver=2013.11.04.gc53aeff
pkgrel=1
pkgdesc="user interface library"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/girara"
license=('custom')
depends=('gtk3>=3.0.0' 'girara-common-git')
makedepends=('git')
conflicts=(girara-gtk$_girara_gtk_version)
provides=(girara-gtk$_girara_gtk_version)
source=("git://pwmt.org/girara.git#branch=develop")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "${srcdir}/${_gitname}"
  make GIRARA_GTK_VERSION=$_girara_gtk_version
}

package() {
  cd "${srcdir}/${_gitname}"

  # install files
  make DESTDIR="$pkgdir" install \
    GIRARA_GTK_VERSION=$_girara_gtk_version

  # remove common files
  rm -r "$pkgdir/usr/include"
  rm -r "$pkgdir/usr/share"

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
