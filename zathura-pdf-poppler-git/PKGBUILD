# Contributor: karnath <karnathtorjian@gmail.com>
# Contributor: gns_ank <anikevicius@gmail.com>

_gitname="zathura-pdf-poppler"

pkgname=zathura-pdf-poppler-git
pkgver=2013.09.29.gd34d04e
pkgrel=1
pkgdesc="PDF support for zathura by using the poppler rendering library"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/plugins/zathura-pdf-poppler"
license=('custom')
depends=('poppler-glib' 'zathura-git')
makedepends=('git')
conflicts=('zathura-pdf-mupdf' 'zathura-pdf-mupdf-git' 'zathura-pdf-poppler')
provides=('zathura-pdf-poppler-git' 'zathura-pdf-poppler')
source=("git://pwmt.org/${_gitname}.git#branch=develop")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  # install files
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
