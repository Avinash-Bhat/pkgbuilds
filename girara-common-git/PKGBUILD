# Maintainer: mlq <mlq@pwmt.org>
# Contributor: gns_ank <anikevicius@gmail.com>

_gitname='girara'

pkgname=girara-common-git
pkgver=2013.11.04.gc53aeff
pkgrel=1
pkgdesc="user interface library (common files)"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/libgirara"
license=('custom')
makedepends=('git')
conflicts=('girara-common')
provides=('girara-common')
source=("git://pwmt.org/girara.git#branch=develop")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

package() {
  cd "${srcdir}/${_gitname}"

  # install header
  make DESTDIR="$pkgdir" install-headers

  # install language files
  make DESTDIR="$pkgdir" -C po install

  # remove pkgconfig files
  rm -r $pkgdir/usr/lib/pkgconfig

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
