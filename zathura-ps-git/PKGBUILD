# Contributor: karnath <karnathtorjian@gmail.com>
# Contributor: gns_ank <anikevicius@gmail.com>

_gitname="zathura-ps"

pkgname=zathura-ps-git
pkgver=2013.05.12.gf8fecd0
pkgrel=1
pkgdesc="PostScript support for zathura"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/plugins/zathura-ps"
license=('custom')
depends=('libspectre' 'zathura-git')
makedepends=('git')
conflicts=('zathura-ps')
provides=('zathura-ps' 'zathura-djvu-ps')
source=("git://pwmt.org/${_gitname}.git#branch=develop")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  # install files
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
