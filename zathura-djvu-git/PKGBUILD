#Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-djvu-git
pkgver=20120716
pkgrel=1
pkgdesc="DjVu support for zathura"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/plugins/zathura-djvu"
license=('custom')
depends=('djvulibre' 'zathura-git')
makedepends=('git')
conflicts=('zathura-djvu')
provides=('zathura-djvu' 'zathura-djvu-git')

_gitname="zathura-djvu"
_gitroot="git://pwmt.org/$_gitname.git"
_branch="develop"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname" && git checkout --track -b $_branch origin/$_branch
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [ -d "$srcdir/$_gitname-build" ]; then
    rm -rf "$srcdir/$_gitname-build"
  fi

  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" || return 1
  cd "$srcdir/$_gitname-build" || return 1

  make
}

package () {
  cd "${srcdir}/$_gitname-build"
  make DESTDIR="$pkgdir" install
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

